Version=4
IconFile=
NumberOfModules=7
Module1=PackagesActivity
Module2=SMSChannelsActivity
Module3=Notification
Module4=Receiver
Module5=WelcomeActivity
Module6=StateManager
Module7=KeyValueStore
Build1=Default,com.moribanxenia.easytopup
DoNotOverwriteManifest=False
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.basic4ppc.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="5" android:targetSdkVersion="14"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~SetApplicationAttribute(android:theme, "@style/MyTheme")~\n~'SetActivityAttribute(WelcomeActivity, android:theme, "@style/MyThemeTransparent")~\n~SetActivityAttribute(WelcomeActivity, android:theme, "@android:style/Theme.Translucent")~\n~~\n~AddPermission("android.permission.CALL_PHONE")~\n~~\n~AddPermission("android.permission.SEND_SMS")~\n~AddPermission("android.permission.RECEIVE_SMS")~\n~~\n~AddPermission("android.permission.READ_SMS")~\n~AddPermission("android.permission.WRITE_SMS")~\n~~\n~SetActivityAttribute(Notification, android:noHistory, "true")~\n~~\n~SetApplicationAttribute(android:name,"anywheresoftware.b4a.objects.ParseObjectWrapper$ParseApplication")~\n~~\n~AddApplicationText(<service android:name="com.parse.PushService" />~\n~<receiver android:name="com.parse.ParseBroadcastReceiver">~\n~  <intent-filter>~\n~    <action android:name="android.intent.action.BOOT_COMPLETED" />~\n~    <action android:name="android.intent.action.USER_PRESENT" />~\n~  </intent-filter>~\n~</receiver>)~\n~~\n~AddReceiverText(Receiver, ~\n~<intent-filter>~\n~    <action android:name="com.moribanxenia.easytopup.RECEIVE" />~\n~</intent-filter>)~\n~~\n~'End of default text.~\n~
NumberOfFiles=24
File1=balance.png
File2=balancetransfer.png
File3=BalanceTransferAndTopMeUpDialog.bal
File4=bonus.png
File5=callcenter.png
File6=callmeback.png
File7=CallMeBackDialog.bal
File8=checkbalance.png
File9=database
File10=ic_action_overflow.png
File11=ListDialog.bal
File12=Main.bal
File13=packages.json
File14=settings.png
File15=SmartZawgyi.ttf
File16=smschannels.json
File17=swipe.png
File18=swipebackground.png
File19=topmeup.png
File20=topup.png
File21=TopUpDialog.bal
File22=ussd.png
File23=vas.png
File24=whatismynumber.png
NumberOfLibraries=9
Library1=core
Library2=phone
Library3=reflection
Library4=stringutils
Library5=json
Library6=contentresolver
Library7=gps
Library8=randomaccessfile
Library9=sql
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: Easy Top Up
	#VersionCode: 1
	#VersionName: 1.0
	#SupportedOrientations: portrait
	#CanInstallToExternalStorage: False
	#AdditionalRes: ..\res
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: False
#End Region

Sub Process_Globals
	Private pm As PackageManager
	Public Const AppName As String = pm.GetApplicationLabel(GetPackageName)
	Public Const AppVersion As String = pm.GetVersionName(GetPackageName)
	Public Const AppPublisher As String = "Kyaw Swar Thwin"
	
	Private kvs As KeyValueStore
	
	Private Parse As Parse
	Private gps1 As GPS
	
	Private Const ID_ACTION_HOME As Int = 0
	Private Const ID_ACTION_OVERFLOW As Int = 99
	
	Private tmrBanner As Timer
	Private iCurrentBanner As Int = 0
	
	Private Const TYPE_HOME As Int = 1
	Private Const TYPE_PLANSANDPACKAGES As Int = 2
	Private Const TYPE_VAS As Int = 3
	Private Const TYPE_STORES As Int = 4
	Private Const TYPE_ABOUT As Int = 5
	
	Public SmartZawgyi As Typeface = Typeface.LoadFromAssets("SmartZawgyi.ttf")
	
	Public Const FILL_PARENT As Int = -1
	Public Const WRAP_CONTENT As Int = -2
	
	Private iCurrentPage As Int
	
	Private p As Phone
	Public SIMOperator As String = p.GetSimOperator
End Sub

Sub Globals
	Dim AppIcon As BitmapDrawable = pm.GetApplicationIcon(GetPackageName)
	
	Dim pi As ParseInstallation
	Dim IsGPSCheck As Boolean = False
	
	Dim xml As XmlLayoutBuilder
	
	Dim ab As AHActionBar
	Dim overflowIcon As BitmapDrawable
	Dim menu As AHPopupMenu
	
	Dim SIMOperatorName As Map = CreateMap("41401": "MPT", "41404": "MPT CDMA", "41403": "MECTel", "41405": "Ooredoo", "41406": "Telenor")
	
	Dim pnlBanner, pnlContent As Panel
	Dim asBanner() As String = Array As String("banner.jpg", "banner2.jpg", "banner3.jpg", "banner4.jpg")
	
	Dim containerBanner, container As AHPageContainer
	Dim pagerBanner, pager As AHViewPager
	Dim tabs As AHViewPagerTabs
	Dim line As Panel
	
	Dim lblDeveloper As Label
	
	Dim ivAppIcon As ImageView
	
	Dim Dialog As DialogView
	
	Dim sCode, sPhoneNumber, sSMSBody As String
	Dim su As StringUtils
	Dim pc As PhoneCalls
	Dim ps As PhoneSms
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Dim pnl As Panel
	Dim iv As ImageView
	
	If FirstTime Then
		If Not (File.Exists(File.DirInternal, "database")) Then File.Copy(File.DirAssets, "database", File.DirInternal, "database")
		kvs.Initialize(File.DirInternal, "database")
		
		Parse.TrackOpening
		Parse.EnableNotifications(Notification)
		Parse.Subscribe("Broadcast", Notification)
		StartService(Receiver)
		gps1.Initialize("GPS")
		If Not (StateManager.GetSetting2("IsRegistered", False)) Then Register
	Else
		Activity.RemoveAllViews
	End If
	
	If FirstTime OR ab.IsInitialized = False Then
		menu.Initialize("AC")
		For i = 0 To SIMOperatorName.Size - 1
			Dim ai As AHActionItem
			ai.Initialize(i, SIMOperatorName.GetValueAt(i), Null)
			menu.AddActionItem(ai)
		Next
	End If
	
	Activity.LoadLayout("Main")
	
	overflowIcon.Initialize(LoadBitmap(File.DirAssets, "ic_action_overflow.png"))
	
	ab.Initialize("AB")
	ab.SubTitle = SIMOperatorName.GetDefault(SIMOperator, "MECTel")
	ab.AddHomeAction(ID_ACTION_HOME, AppIcon)
	ab.AddAction(ID_ACTION_OVERFLOW, overflowIcon)
	Activity.AddView(ab, 0, 0, 100%x, 48dip)
	
	containerBanner.Initialize
	For i = 0 To asBanner.Length - 1
		pnl.Initialize("")
		iv.Initialize("")
		iv.Bitmap = kvs.GetBitmap(asBanner(i))
		iv.Gravity = Gravity.FILL
		pnl.AddView(iv, 0, 0, FILL_PARENT, FILL_PARENT)
		containerBanner.AddPage(pnl, "")
	Next
	pagerBanner.Initialize(containerBanner, "Banner")
	pnlBanner.AddView(pagerBanner, 0, 0, 100%x, 37%x)
	tmrBanner.Initialize("tmrBanner", 8000)
	tmrBanner.Enabled = True
	
	container.Initialize
	For i = 0 To 4
		Select i
			Case 0
				pnl = CreatePanel(TYPE_HOME)
				container.AddPage(pnl, "Home")
			Case 1
				If SIMOperator <> "41404" Then
					pnl = CreatePanel(TYPE_PLANSANDPACKAGES)
					container.AddPage(pnl, "Plans & Packages")
				End If
			Case 2
				pnl = CreatePanel(TYPE_VAS)
				container.AddPage(pnl, "VAS")
			Case 3
				pnl = CreatePanel(TYPE_STORES)
				container.AddPage(pnl, "Stores")
			Case 4
				pnl = CreatePanel(TYPE_ABOUT)
				container.AddPage(pnl, "About")
		End Select
	Next
	
	pager.Initialize(container, "Pager")
	
	tabs.Initialize(pager)
	tabs.LineHeight = 5dip
	tabs.UpperCaseTitle = True
	tabs.TextColor = Colors.LightGray
	tabs.TextColorCenter = Colors.DarkGray
	tabs.LineColorCenter = Colors.DarkGray
	tabs.BackgroundColorPressed = Colors.RGB(51, 181, 229)
	pnlContent.AddView(tabs, 0, 0, FILL_PARENT, WRAP_CONTENT)
	
	line.Initialize("")
	line.Color = tabs.LineColorCenter
	pnlContent.AddView(line, 0, tabs.Top + tabs.Height + 35dip, pnlContent.Width, 2dip)
	
	pnlContent.AddView(pager, 0, line.Top + line.Height, pnlContent.Width, pnlContent.Height - (line.Top + line.Height))
	
	lblDeveloper.Text = "Developed By: " & AppPublisher
End Sub

Sub Activity_Resume
	Activity.Title = AppName
	
	pagerBanner.GotoPage(iCurrentBanner, False)
	pager.GotoPage(iCurrentPage, False)
	
	If StateManager.GetSetting2("FirstTime", True) Then
		StartActivity(WelcomeActivity)
	Else If gps1.GPSEnabled = False AND IsGPSCheck = False Then
		IsGPSCheck = True
		Dialog.Options.Elements.Title.Typeface = SmartZawgyi
		Dialog.Options.Elements.Message.Typeface = SmartZawgyi
		Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
		If Dialog.Msgbox("သတိေပးခ်က္", "ပ႐ိုမိုးရွင္း ႏွင့္ ကံစမ္းမဲအစီအစဥ္မ်ားအတြက္ GPS ဖြင့္ထားေပးရန္ လိုအပ္ပါသည္။", "အိုေက", "", "ပယ္ဖ်က္ပါ", Null) = DialogResponse.POSITIVE Then StartActivity(gps1.LocationSettingsIntent)
	End If
End Sub

Sub Activity_KeyPress (KeyCode As Int) As Boolean 'Return True to consume the event
	If KeyCode = KeyCodes.KEYCODE_MENU Then AB_ItemClicked(ID_ACTION_OVERFLOW)
End Sub

Sub Activity_Pause (UserClosed As Boolean)
	StateManager.SaveSettings
End Sub

Sub Register
	Log("Registering")
	Dim pid As PhoneId
	pi = pi.GetCurrentInstallation
	pi.Put("Manufacturer", p.Manufacturer)
	pi.Put("ModelNumber", p.Model)
	pi.Put("IMEI", pid.GetDeviceId)
	pi.Put("IMSI", pid.GetSubscriberId)
	pi.SaveEventually("Parse", 1)
End Sub

Sub Parse_DoneSave(Success As Boolean, TaskID As Int)
	Log(Success & ":" & TaskID)
	Select TaskID
		Case 1
			If Success Then
				gps1.Start(0, 0)
			Else
				Register
			End If
		Case 2
			If Success Then
				Log("Registered")
				StateManager.SetSetting("IsRegistered", True)
			Else
				gps1.Start(0, 0)
			End If
	End Select
End Sub

Sub GPS_LocationChanged (Location1 As Location)
	gps1.Stop
	Dim pgp As ParseGeoPoint
	pi = pi.GetCurrentInstallation
	pgp.Initialize(Location1.Latitude, Location1.Longitude)
	pi.Put("Location", pgp)
	pi.SaveEventually("Parse", 2)
End Sub

Sub AB_ItemClicked(ItemID As Int)
	Select ItemID
		Case ID_ACTION_OVERFLOW
			menu.Show(ab.GetActionView(ItemID))
	End Select
End Sub

Sub AC_Click (Position As Int, ActionItemID As Int)
	Dim sOldSIMOperator As String = SIMOperator
	SIMOperator = SIMOperatorName.GetKeyAt(Position)
	If sOldSIMOperator <> SIMOperator Then Activity_Create(False)
End Sub

Sub tmrBanner_Tick
	iCurrentBanner = iCurrentBanner + 1
	If iCurrentBanner > asBanner.Length - 1 Then iCurrentBanner = 0
	pagerBanner.GotoPage(iCurrentBanner, True)
End Sub

Sub Banner_PageChanged (Position As Int)
	iCurrentBanner = Position
End Sub

Sub CreatePanel(PanelType As Int) As Panel
	Dim pnl As Panel
	Dim lvwHome, lvwPlansAndPackages, lvwVAS, lvwStores, lvwAbout As ListView
	Dim lblAppName, lblAppVersion As Label
	
	pnl.Initialize("")
	Select PanelType
		Case TYPE_HOME
			lvwHome.Initialize("lvwHome")
			lvwHome.TwoLinesAndBitmap.Label.TextColor = Colors.DarkGray
			lvwHome.TwoLinesAndBitmap.SecondLabel.Typeface = SmartZawgyi
			If SIMOperator = "41401" OR SIMOperator = "41405" OR SIMOperator = "41406" Then lvwHome.AddTwoLinesAndBitmap2("What's My Number?", "မိမိဖုန္းနံပါတ္ကိုစစ္ေဆးရန္", LoadBitmap(File.DirAssets, "whatismynumber.png"), 1)
			lvwHome.AddTwoLinesAndBitmap2("Check Balance", "ဖုန္းလက္က်န္ေငြစစ္ေဆးရန္", LoadBitmap(File.DirAssets, "checkbalance.png"), 2)
			lvwHome.AddTwoLinesAndBitmap2("Top Up", "ဖုန္းေငြျဖည့္ရန္", LoadBitmap(File.DirAssets, "topup.png"), 3)
			If SIMOperator = "41405" OR SIMOperator = "41406" Then lvwHome.AddTwoLinesAndBitmap2("Balance Transfer", "ဖုန္းလက္က်န္ေငြလႊဲေျပာင္းေပးရန္", LoadBitmap(File.DirAssets, "balancetransfer.png"), 4)
			If SIMOperator = "41405" Then lvwHome.AddTwoLinesAndBitmap2("Top Me Up", "မိမိဖုန္းအားေငြျဖည့္ေပးပါရန္", LoadBitmap(File.DirAssets, "topmeup.png"), 5)
			If SIMOperator = "41405" OR SIMOperator = "41406" Then lvwHome.AddTwoLinesAndBitmap2("Call Me Back", "မိမိဖုန္းအားျပန္ေခၚေပးပါရန္", LoadBitmap(File.DirAssets, "callmeback.png"), 6)
			If SIMOperator = "41405" OR SIMOperator = "41406" Then lvwHome.AddTwoLinesAndBitmap2("USSD", "ဝန္ေဆာင္မႈမ်ားကိုစီမံရန္", LoadBitmap(File.DirAssets, "ussd.png"), 7)
			lvwHome.AddTwoLinesAndBitmap2("Call Center", "အေထြေထြဝန္ေဆာင္မႈမ်ားကိုစံုစမ္းေမးျမန္းရန္", LoadBitmap(File.DirAssets, "callcenter.png"), 8)
			pnl.AddView(lvwHome, 0, 0, FILL_PARENT, FILL_PARENT)
		Case TYPE_PLANSANDPACKAGES
			lvwPlansAndPackages.Initialize("lvwPlansAndPackages")
			lvwPlansAndPackages.TwoLinesAndBitmap.Label.TextColor = Colors.DarkGray
			lvwPlansAndPackages.TwoLinesAndBitmap.SecondLabel.Typeface = SmartZawgyi
			If SIMOperator = "41401" Then lvwPlansAndPackages.AddTwoLinesAndBitmap2("Swe Thahar Plan", "ေဆြသဟာအစီအစဥ္", LoadBitmap(File.DirAssets, "vas.png"), 1)
			If SIMOperator = "41401" Then lvwPlansAndPackages.AddTwoLinesAndBitmap2("Voice Packages", "Voice Package မ်ား", LoadBitmap(File.DirAssets, "vas.png"), 2)
			If SIMOperator = "41401" OR SIMOperator = "41405" OR SIMOperator = "41406" Then lvwPlansAndPackages.AddTwoLinesAndBitmap2("Internet Packages", "အင္တာနက္ Package မ်ား", LoadBitmap(File.DirAssets, "vas.png"), 3)
			If SIMOperator <> "41404" AND SIMOperator <> "41405" Then lvwPlansAndPackages.AddTwoLinesAndBitmap2("Internet Service", "အင္တာနက္ဝန္ေဆာင္မႈ", LoadBitmap(File.DirAssets, "vas.png"), 4)
			If SIMOperator = "41405" OR SIMOperator = "41406" Then lvwPlansAndPackages.AddTwoLinesAndBitmap2("Send Me APN Settings", "မိမိဖုန္းအား APN Setting မ်ားပို႔ေပးပါရန္", LoadBitmap(File.DirAssets, "settings.png"), 5)
			pnl.AddView(lvwPlansAndPackages, 0, 0, FILL_PARENT, FILL_PARENT)
		Case TYPE_VAS
			lvwVAS.Initialize("lvwVAS")
			lvwVAS.TwoLinesAndBitmap.Label.TextColor = Colors.DarkGray
			lvwVAS.TwoLinesAndBitmap.SecondLabel.Typeface = SmartZawgyi
			Select SIMOperator
				Case "41401", "41404"' MPT, MPT CDMA
				Case "41405"' Ooredoo
					lvwVAS.AddTwoLinesAndBitmap2("FunTone", "FunTone", LoadBitmap(File.DirAssets, "vas.png"), 1)
				Case "41406"' Telenor
					lvwVAS.AddTwoLinesAndBitmap2("My Tune", "My Tune", LoadBitmap(File.DirAssets, "vas.png"), 1)
				Case Else' MECTel
					lvwVAS.AddTwoLinesAndBitmap2("Hello Music", "Hello Music", LoadBitmap(File.DirAssets, "vas.png"), 1)
			End Select
			If SIMOperator = "41405" OR SIMOperator = "41406" Then lvwVAS.AddTwoLinesAndBitmap2("SMS Channels", "SMS Channel မ်ား", LoadBitmap(File.DirAssets, "vas.png"), 2)
			If SIMOperator = "41405" Then lvwVAS.AddTwoLinesAndBitmap2("CLIR", "CLIR", LoadBitmap(File.DirAssets, "vas.png"), 3)
			If SIMOperator = "41401" OR SIMOperator = "41404" Then lvwVAS.AddTwoLinesAndBitmap2("Call Forwarding", "Call Forwarding", LoadBitmap(File.DirAssets, "vas.png"), 4)
			If SIMOperator = "41401" OR SIMOperator = "41404" Then lvwVAS.AddTwoLinesAndBitmap2("Call Waiting", "Call Waiting", LoadBitmap(File.DirAssets, "vas.png"), 5)
			If SIMOperator <> "41404" AND SIMOperator <> "41405" Then lvwVAS.AddTwoLinesAndBitmap2("Missed Call Alert", "Missed Call Alert", LoadBitmap(File.DirAssets, "vas.png"), 6)
			If SIMOperator <> "41404" AND SIMOperator <> "41405" Then lvwVAS.AddTwoLinesAndBitmap2("Voice Mail", "Voice Mail", LoadBitmap(File.DirAssets, "vas.png"), 7)
			pnl.AddView(lvwVAS, 0, 0, FILL_PARENT, FILL_PARENT)
		Case TYPE_STORES
			lvwStores.Initialize("lvwStores")
			lvwStores.TwoLinesLayout.ItemHeight = 121dip
			lvwStores.TwoLinesLayout.Label.Top = 15dip
			lvwStores.TwoLinesLayout.Label.Typeface = SmartZawgyi
			lvwStores.TwoLinesLayout.Label.Gravity = Gravity.CENTER_HORIZONTAL
			lvwStores.TwoLinesLayout.Label.TextColor = Colors.DarkGray
			lvwStores.TwoLinesLayout.SecondLabel.Top = lvwStores.TwoLinesLayout.Label.Top + lvwStores.TwoLinesLayout.Label.Height + 2dip
			lvwStores.TwoLinesLayout.SecondLabel.Height = 59dip
			lvwStores.TwoLinesLayout.SecondLabel.Typeface = SmartZawgyi
			lvwStores.TwoLinesLayout.SecondLabel.Gravity = Gravity.CENTER_HORIZONTAL
			lvwStores.AddTwoLines2("မေကြးေရွာ့ပင္းေမာ(လ္)", "အမွတ္(၈)၊ ဗိုလ္ခ်ဳပ္လမ္း၊ " & CRLF & "ပြဲႀကိဳရပ္ကြက္၊ မေကြးၿမိဳ႕။" & CRLF & "ဖုန္း ၀၆၃-၂၆၇၁၁၊ ၀၆၃-၂၇၂၂၃။", 1)
			lvwStores.AddTwoLines2("ေရႊဧရာ (မေကြး-၁)", "အမွတ္(၁၀)၊ ျပည္ေတာ္သာလမ္း၊ " & CRLF & "ေဈးလယ္စိုးရပ္ကြက္၊ မေကြးၿမိဳ႕။" & CRLF & "ဖုန္း ၀၆၃-၂၅၄၅၂၊ ၀၆၃-၂၆၁၇၁။", 2)
			lvwStores.AddTwoLines2("ေရႊဧရာ (မေကြး-၂)", "အမွတ္(၃၆)၊ ဗိုလ္ခ်ဳပ္လမ္း ႏွင့္ မဲထီးလမ္းေထာင့္၊ " & CRLF & "ရြာသစ္ရပ္ကြက္၊ မေကြးၿမိဳ႕။" & CRLF & "ဖုန္း ၀၆၃-၂၃၆၉၂၊ ၀၆၃-၂၈၁၉၃။", 3)
			lvwStores.AddTwoLines2("ေရႊဧရာ (မင္းဘူး)", "အမွတ္(၈၁၂)၊ မင္းဘူး-စကုလမ္း၊ " & CRLF & "အမွတ္(၃)ရပ္ကြက္၊ မင္းဘူးၿမိဳ႕။" & CRLF & "ဖုန္း ၀၆၅-၂၁၅၄၇။", 4)
			pnl.AddView(lvwStores, 0, 0, FILL_PARENT, FILL_PARENT)
		Case TYPE_ABOUT
			ivAppIcon.Initialize("")
			ivAppIcon.Bitmap = AppIcon.Bitmap
			ivAppIcon.Gravity = Gravity.FILL
			pnl.AddView(ivAppIcon, 50%x - 50dip, 10dip, 100dip, 100dip)
			
			lblAppName.Initialize("")
			lblAppName.Text = AppName
			lblAppName.Gravity = Gravity.CENTER
			lblAppName.TextSize = 19.5
			lblAppName.TextColor = Colors.DarkGray
			pnl.AddView(lblAppName, 0, ivAppIcon.Top + ivAppIcon.Height + 10dip, 100%x, WRAP_CONTENT)
			
			lblAppVersion.Initialize("")
			lblAppVersion.Text = "Version: " & AppVersion
			lblAppVersion.Gravity = Gravity.CENTER
			lblAppVersion.TextSize = 16.5
			lblAppVersion.TextColor = -7829368
			pnl.AddView(lblAppVersion, 0, lblAppName.Top + su.MeasureMultilineTextHeight(lblAppName, lblAppName.Text), 100%x, WRAP_CONTENT)
			
			line.Initialize("")
			line.Color = Colors.LightGray
			pnl.AddView(line, 0, lblAppVersion.Top + su.MeasureMultilineTextHeight(lblAppVersion, lblAppVersion.Text) + 10dip, FILL_PARENT, 1dip)
			
			lvwAbout.Initialize("lvwAbout")
			lvwAbout.SingleLineLayout.Label.Gravity = Gravity.CENTER
			lvwAbout.SingleLineLayout.Label.TextSize = lblAppVersion.TextSize
			lvwAbout.SingleLineLayout.Label.TextColor = Colors.DarkGray
			lvwAbout.AddSingleLine2("Developer: " & AppPublisher, 1)
			lvwAbout.AddSingleLine2("Contact: 09-450049060, 09-43165226", 2)
			pnl.AddView(lvwAbout, 0, line.Top + line.Height, FILL_PARENT, FILL_PARENT)
	End Select
	
	Return pnl
End Sub

Sub Pager_PageChanged (Position As Int)
	iCurrentPage = Position
	pager.RequestFocus
	If Position = 3 OR Position = 4 Then
		Dim ap, ap2 As AnimationPlus
		Dim a As AnimationSet
		ap.InitializeAlpha("", 0, 1)
		ap.StartOffset = 500
		ap.Duration = 800
		ap2.InitializeScaleCenter("", 0, 0, 1, 1, ivAppIcon)
		ap2.Duration = 1500
		ap2.SetInterpolator(ap2.INTERPOLATOR_OVERSHOOT)
		a.Initialize(False)
		a.AddAnimation(ap)
		a.AddAnimation(ap2)
		a.PersistAfter = True
		a.Start(ivAppIcon)
	End If
End Sub

Sub lvwHome_ItemClick (Position As Int, Value As Object)
	Select Value
		Case 1 : WhatIsMyNumber
		Case 2 : CheckBalance
		Case 3 : TopUp
		Case 4 : BalanceTransfer
		Case 5 : TopMeUp
		Case 6 : CallMeBack
		Case 7 : USSD
		Case 8 : CallCenter
	End Select
End Sub

Sub WhatIsMyNumber
	Select SIMOperator
		Case "41401" : sCode = "*888#"' MPT
		Case "41405" : sCode = "*133*5#"' Ooredoo
		Case "41406" : sCode = "*97#"' Telenor
	End Select
	StartActivity(pc.Call(su.EncodeUrl(sCode, "UTF8")))
End Sub

Sub CheckBalance
	Dim iResult As Int = 0
	
	sCode = ""
	
	If SIMOperator = "41401" OR SIMOperator = "41404" OR SIMOperator = "41405" OR SIMOperator = "41406" Then
		Dialog.Options.Dimensions.Width = 100%x
		Dialog.Options.Dimensions.Height = 121dip
		Dialog.Options.Elements.Title.Typeface = SmartZawgyi
		Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
		Dim DialogLayout As DialogViewLayout = Dialog.LoadLayout("ListDialog")
		Dim lvwList As ListView = DialogLayout.Views.Get("lvwList")
		
		lvwList.TwoLinesAndBitmap.Label.Height = 60dip
		lvwList.TwoLinesAndBitmap.Label.Typeface = SmartZawgyi
		lvwList.TwoLinesAndBitmap.Label.Gravity = Gravity.CENTER_VERTICAL
		lvwList.TwoLinesAndBitmap.Label.TextColor = Colors.DarkGray
		
		lvwList.AddTwoLinesAndBitmap("ဖုန္းလက္က်န္ေငြ", "", LoadBitmap(File.DirAssets, "balance.png"))
		lvwList.AddTwoLinesAndBitmap("အင္တာနက္ႏွင့္အပိုဆုမ်ား", "", LoadBitmap(File.DirAssets, "bonus.png"))
		
		iResult = DialogLayout.Show("ဖုန္းလက္က်န္ေငြစစ္ေဆးရန္", "", "", "ပယ္ဖ်က္ပါ", Null)
		If iResult = DialogResponse.CANCEL Then Return
	End If
	If iResult = 0 Then
		Select SIMOperator
			Case "41401", "41405", "41406" : sCode = "*124#"' MPT, Ooredoo, Telenor
			Case "41404" : sCode = "*162"' MPT CDMA
			Case Else : sCode = "*123#"' MECTel
		End Select
	Else
		Select SIMOperator
			Case "41401", "41404" : sPhoneNumber = "1331" : sSMSBody = "QER"' MPT, MPT CDMA
			Case "41405" : sPhoneNumber = "2230" : sSMSBody = "b"' Ooredoo
			Case "41406" : sCode = "*124*1#"' Telenor
		End Select
	End If
	If sCode = "" Then
		ps.Send(sPhoneNumber, sSMSBody)
	Else
		StartActivity(pc.Call(su.EncodeUrl(sCode, "UTF8")))
	End If
End Sub

Sub TopUp
	Dialog.Options.Dimensions.Width = 100%x
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Dim DialogLayout As DialogViewLayout = Dialog.LoadLayout("TopUpDialog")
	Dim optTopUpSelf As RadioButton = DialogLayout.Views.Get("optTopUpSelf")
	Dim optTopUpOther As RadioButton = DialogLayout.Views.Get("optTopUpOther")
	Dim lblPIN As Label = DialogLayout.Views.Get("lblPIN")
	Dim edtPIN As EditText = DialogLayout.Views.Get("edtPIN")
	Dim lblPhoneNumber As Label = DialogLayout.Views.Get("lblPhoneNumber")
	Dim edtPhoneNumber As EditText = DialogLayout.Views.Get("edtPhoneNumber")
	Dim btnContactPicker As Button = DialogLayout.Views.Get("btnContactPicker")
	
	DialogLayout.Views.Panel("pnl").Height = edtPhoneNumber.Top + edtPhoneNumber.Height + 10dip
	
	Select SIMOperator
		Case "41401", "41405", "41406" : sCode = "*123*"' MPT, Ooredoo, Telenor
		Case "41404" : sCode = "*166*"' MPT CDMA
		Case Else : sCode = "*124*"' MECTel
	End Select
	
	optTopUpSelf.Text = "မိမိဖုန္း"
	
	If SIMOperator <> "41401" AND SIMOperator <> "41405" AND SIMOperator <> "41406" Then optTopUpOther.Enabled = False
	optTopUpOther.Text = "အျခားဖုန္း"
	
	lblPIN.Text = "Top Up ကတ္မွ PIN နံပါတ္႐ိုက္ထည့္ပါ။"
	
	lblPhoneNumber.Text = "ေငြျဖည့္ေပးလိုေသာဖုန္းနံပါတ္႐ိုက္ထည့္ပါ။"
	
	btnContactPicker.Background = xml.GetDrawable("17301547")
	
	Do Until (optTopUpSelf.Checked = True AND edtPIN.Text.Length > 0) OR (optTopUpOther.Checked = True AND (edtPIN.Text.Length > 0 AND edtPhoneNumber.Text.Length > 0))
		If DialogLayout.Show("ဖုန္းေငြျဖည့္ရန္", "ေငြျဖည့္ပါ", "", "ပယ္ဖ်က္ပါ", Null) = DialogResponse.CANCEL Then Return
		If edtPIN.Text.Length = 0 Then
			edtPIN.RequestFocus
			ToastMessageShow("PIN နံပါတ္ေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		Else If optTopUpOther.Checked = True AND edtPhoneNumber.Text.Length = 0 Then
			edtPhoneNumber.RequestFocus
			ToastMessageShow("ဖုန္းနံပါတ္ေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		End If
	Loop
	If optTopUpSelf.Checked Then
		StartActivity(pc.Call(su.EncodeUrl(sCode & edtPIN.Text & "#", "UTF8")))
	Else If optTopUpOther.Checked Then
		If SIMOperator = "41405" Then' Ooredoo
			StartActivity(pc.Call(su.EncodeUrl(sCode & edtPhoneNumber.Text & "*" & edtPIN.Text & "*1#", "UTF8")))
		Else
			StartActivity(pc.Call(su.EncodeUrl(sCode & edtPIN.Text & "*" & edtPhoneNumber.Text & "#", "UTF8")))
		End If
	End If
End Sub

Sub optTopUpSelf_CheckedChange(Checked As Boolean)
	If Checked Then
		Dialog.Views.Label("lblPhoneNumber").Visible = False
		Dialog.Views.EditText("edtPhoneNumber").Visible = False
		Dialog.Views.Button("btnContactPicker").Visible = False
	End If
End Sub

Sub optTopUpOther_CheckedChange(Checked As Boolean)
	If Checked Then
		Dialog.Views.Label("lblPhoneNumber").Visible = True
		Dialog.Views.EditText("edtPhoneNumber").Visible = True
		Dialog.Views.Button("btnContactPicker").Visible = True
	End If
End Sub

Sub btnContactPicker_Click
	Dim cp As ContactPicker
	cp.Initialize("cp")
	cp.Show
End Sub

Sub cp_Result (Success As Boolean, DisplayName As String, PhoneNumber As String, PhoneType As Int)
	If Success Then
		Dialog.Views.EditText("edtPhoneNumber").RequestFocus
		Dialog.Views.EditText("edtPhoneNumber").Text = PhoneNumber
	End If
End Sub

Sub BalanceTransfer
	Dialog.Options.Dimensions.Width = 100%x
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Dim DialogLayout As DialogViewLayout = Dialog.LoadLayout("BalanceTransferAndTopMeUpDialog")
	Dim lblPhoneNumber As Label = DialogLayout.Views.Get("lblPhoneNumber")
	Dim edtPhoneNumber As EditText = DialogLayout.Views.Get("edtPhoneNumber")
	Dim btnContactPicker As Button = DialogLayout.Views.Get("btnContactPicker")
	Dim lblAmount As Label = DialogLayout.Views.Get("lblAmount")
	Dim edtAmount As EditText = DialogLayout.Views.Get("edtAmount")
	
	DialogLayout.Views.Panel("pnl").Height = edtAmount.Top + edtAmount.Height + 10dip
	
	Select SIMOperator
		Case "41405" : sCode = "*155*"' Ooredoo
		Case "41406" : sCode = "*979*2*4*"' Telenor
	End Select
	
	lblPhoneNumber.Text = "ေငြလက္ခံမည့္သူ၏ဖုန္းနံပါတ္႐ိုက္ထည့္ပါ။"
	
	btnContactPicker.Background = xml.GetDrawable("17301547")
	
	lblAmount.Text = "လႊဲေျပာင္းေပးလိုေသာေငြပမာဏ႐ိုက္ထည့္ပါ။"
	
	Do Until edtPhoneNumber.Text.Length > 0 AND edtAmount.Text.Length > 0
		If DialogLayout.Show("ဖုန္းလက္က်န္ေငြလႊဲေျပာင္းေပးရန္", "ေငြလႊဲပါ", "", "ပယ္ဖ်က္ပါ", Null) = DialogResponse.CANCEL Then Return
		If edtPhoneNumber.Text.Length = 0 Then
			edtPhoneNumber.RequestFocus
			ToastMessageShow("ဖုန္းနံပါတ္ေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		Else If edtAmount.Text.Length = 0 Then
			edtAmount.RequestFocus
			ToastMessageShow("ေငြပမာဏေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		End If
	Loop
		If SIMOperator = "41405" Then' Ooredoo
			StartActivity(pc.Call(su.EncodeUrl(sCode & edtAmount.Text & "*" & edtPhoneNumber.Text & "*1#", "UTF8")))
		Else
			StartActivity(pc.Call(su.EncodeUrl(sCode & edtPhoneNumber.Text & "*" & edtAmount.Text & "*1#", "UTF8")))
		End If
End Sub

Sub TopMeUp
	Dialog.Options.Dimensions.Width = 100%x
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Dim DialogLayout As DialogViewLayout = Dialog.LoadLayout("BalanceTransferAndTopMeUpDialog")
	Dim lblPhoneNumber As Label = DialogLayout.Views.Get("lblPhoneNumber")
	Dim edtPhoneNumber As EditText = DialogLayout.Views.Get("edtPhoneNumber")
	Dim btnContactPicker As Button = DialogLayout.Views.Get("btnContactPicker")
	Dim lblAmount As Label = DialogLayout.Views.Get("lblAmount")
	Dim edtAmount As EditText = DialogLayout.Views.Get("edtAmount")
	
	DialogLayout.Views.Panel("pnl").Height = edtAmount.Top + edtAmount.Height + 10dip
	
	Select SIMOperator
		Case "41405" : sCode = "*126*"' Ooredoo
	End Select
	
	lblPhoneNumber.Text = "ေငြျဖည့္ေပးမည့္သူ၏ဖုန္းနံပါတ္႐ိုက္ထည့္ပါ။"
	
	btnContactPicker.Background = xml.GetDrawable("17301547")
	
	lblAmount.Text = "ျဖည့္ေပးေစလိုေသာေငြပမာဏ႐ိုက္ထည့္ပါ။"
	
	Do Until edtPhoneNumber.Text.Length > 0 AND edtAmount.Text.Length > 0
		If DialogLayout.Show("မိမိဖုန္းအားေငြျဖည့္ေပးပါရန္", "ပို႔ပါ", "", "ပယ္ဖ်က္ပါ", Null) = DialogResponse.CANCEL Then Return
		If edtPhoneNumber.Text.Length = 0 Then
			edtPhoneNumber.RequestFocus
			ToastMessageShow("ဖုန္းနံပါတ္ေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		Else If edtAmount.Text.Length = 0 Then
			edtAmount.RequestFocus
			ToastMessageShow("ေငြပမာဏေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		End If
	Loop
	StartActivity(pc.Call(su.EncodeUrl(sCode & edtPhoneNumber.Text & "*" & edtAmount.Text & "#", "UTF8")))
End Sub

Sub CallMeBack
	Dialog.Options.Dimensions.Width = 100%x
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Dim DialogLayout As DialogViewLayout = Dialog.LoadLayout("CallMeBackDialog")
	Dim lblPhoneNumber As Label = DialogLayout.Views.Get("lblPhoneNumber")
	Dim edtPhoneNumber As EditText = DialogLayout.Views.Get("edtPhoneNumber")
	Dim btnContactPicker As Button = DialogLayout.Views.Get("btnContactPicker")
	
	DialogLayout.Views.Panel("pnl").Height = edtPhoneNumber.Top + edtPhoneNumber.Height + 10dip
	
	Select SIMOperator
		Case "41405" : sCode = "*122*"' Ooredoo
		Case "41406" : sCode = "*979*3*2*"' Telenor
	End Select
	
	lblPhoneNumber.Text = "ျပန္ေခၚေစလိုေသာဖုန္းနံပါတ္႐ိုက္ထည့္ပါ။"
	
	btnContactPicker.Background = xml.GetDrawable("17301547")
	
	Do Until edtPhoneNumber.Text.Length > 0
		If DialogLayout.Show("မိမိဖုန္းအားျပန္ေခၚေပးပါရန္", "ပို႔ပါ", "", "ပယ္ဖ်က္ပါ", Null) = DialogResponse.CANCEL Then Return
		If edtPhoneNumber.Text.Length = 0 Then
			edtPhoneNumber.RequestFocus
			ToastMessageShow("ဖုန္းနံပါတ္ေနရာကို ကြက္လပ္ထား၍မရပါ။", True)
		End If
	Loop
	StartActivity(pc.Call(su.EncodeUrl(sCode & edtPhoneNumber.Text & "#", "UTF8")))
End Sub

Sub USSD
	Select SIMOperator
		Case "41405" : sCode = "*133#"' Ooredoo
		Case "41406" : sCode = "*979#"' Telenor
	End Select
	StartActivity(pc.Call(su.EncodeUrl(sCode, "UTF8")))
End Sub

Sub CallCenter
	Select SIMOperator
		Case "41401", "41404" : Call(Array As String("106"))' MPT, MPT CDMA
		Case "41405" : Call(Array As String("234", "09970000234"))' Ooredoo
		Case "41406" : Call(Array As String("979", "09790097900"))' Telenor
		Case Else : Call(Array As String("1212"))' MECTel
	End Select
End Sub

Sub lvwList_ItemClick (Position As Int, Value As Object)
	Dialog.Dismiss(Position)
End Sub

Sub lvwPlansAndPackages_ItemClick (Position As Int, Value As Object)
	Select Value
		Case 1 : SweThaharPlan
		Case 2 : ShowPackages("Voice Packages", "voicePackages")
		Case 3 : ShowPackages("Internet Packages", "internetPackages")
		Case 4 : InternetService
		Case 5 : SendMeAPNSettings
	End Select
End Sub

Sub SweThaharPlan
	Plan("ေဆြသဟာအစီအစဥ္", "ဤအစီအစဥ္ကို အသံုးျပဳျခင္းအားျဖင့္<br>- ျပည္တြင္းဖုန္းေခၚဆိုမႈမ်ားအတြက္ တစ္မိနစ္လွ်င္ ၃၅ က်ပ္၊<br>- SMS တစ္ေစာင္ ေပးပို႔လွ်င္ ၁၅ က်ပ္၊<br>- အင္တာနက္အသံုးျပဳခမွာ တစ္မီဂါဘိုက္လွ်င္ ၁၅ က်ပ္ႏႈန္းသာ က်သင့္မည္ျဖစ္သည္။<br>- အက်ံဳးဝင္သည့္ကာလမွာ ရက္ ၃၀ ျဖစ္ၿပီး<br>- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္ပါသည္။", "1332", "SWE", "UNSUB SWE")
End Sub

Sub ShowPackages(Title As String, Package As String)
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Message.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Select SIMOperator
		Case "41401"' MPT
			If Dialog.Msgbox("သတိေပးခ်က္", "- ဤဝန္ေဆာင္မႈကို အသံုးျပဳရန္အတြက္ ေဆြသဟာအစီအစဥ္ ရယူထားရန္ လိုအပ္ပါသည္။", "အိုေက", "", "ပယ္ဖ်က္ပါ", Null) = DialogResponse.CANCEL Then Return
	End Select
	PackagesActivity.Title = Title
	PackagesActivity.Package = Package
	StartActivity(PackagesActivity)
End Sub

Sub InternetService
	Dim sName, sDescription, sPhoneNumber, sActivation, sDeactivation As String
	
	sName = "အင္တာနက္ဝန္ေဆာင္မႈ"
	Select SIMOperator
		Case "41401"' MPT
			sDescription = "- ဤဝန္ေဆာင္မႈသည္ အင္တာနက္အသံုးျပဳမႈ စတင္ရန္အတြက္ ျဖစ္သည္။<br>- ဝန္ေဆာင္ခ ၁၀၀၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ ေကာက္ခံသြားပါလိမ့္မည္။"
			sPhoneNumber = "1331"
			sActivation = "Orderdata service"
		Case "41406"' Telenor
			sDescription = "- ဤဝန္ေဆာင္မႈသည္ အင္တာနက္အသံုးျပဳမႈ ရပ္ဆိုင္းရန္ သို႔မဟုတ္ ျပန္လည္အသံုးျပဳရန္အတြက္ ျဖစ္သည္။<br>- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္သည္။"
			sPhoneNumber = "500"
			sActivation = "internet on"
			sDeactivation = "internet off"
		Case Else' MECTel
			sDescription = "- ဤဝန္ေဆာင္မႈသည္ အင္တာနက္အသံုးျပဳမႈ စတင္ရန္အတြက္ ျဖစ္သည္။<br>- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္သည္။"
			sPhoneNumber = "233"
			sActivation = "Open EVDO"
	End Select
	
	Plan(sName, sDescription, sPhoneNumber, sActivation, sDeactivation)
End Sub

Sub SendMeAPNSettings
	Select SIMOperator
		Case "41405" : sCode = "*133*6*1#"' Ooredoo
		Case "41406" : sCode = "*979*3*1*3#"' Telenor
	End Select
	StartActivity(pc.Call(su.EncodeUrl(sCode, "UTF8")))
End Sub

Sub lvwVAS_ItemClick (Position As Int, Value As Object)
	Select Value
		Case 1 : RingbackTone
		Case 2 : SMSChannels
		Case 3 : CLIR
		Case 4 : CallForwarding
		Case 5 : CallWaiting
		Case 6 : MissedCallAlert
		Case 7 : VoiceMail
	End Select
End Sub

Sub RingbackTone
	Dim sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription As String
	
	sDescription = "- ဤဝန္ေဆာင္မႈသည္ မိတ္ေဆြထံသို႔ ဖုန္းေခၚဆိုသူမ်ားသည္ ျပန္လည္ေျဖဆိုမႈကို ေစာင့္ဆိုင္းေနစဥ္အတြင္း မိတ္ေဆြေရြးခ်ယ္သတ္မွတ္ထားေသာ ဂီတသံစဥ္ကို ခံစားနားဆင္ေနႏိုင္ပါသည္။<br>"
	Select SIMOperator
		Case "41405"' Ooredoo
			sName = "FunTone"
			sDescription = sDescription & "- တစ္ရက္စာဝန္ေဆာင္ခ ၇၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ ေန႔စဥ္ေကာက္ခံသြားပါလိမ့္မည္။<br>- ေတးသြားတစ္ပုဒ္လွ်င္ လစဥ္ ၇၅၀ က်ပ္ႏႈန္းျဖင့္ ငွားရမ္းႏိုင္ပါသည္။<br>- FunTone IVR နံပါတ္ ၃၃၃၃ သို႔ တစ္မိနစ္လွ်င္ ၅၀ က်ပ္ႏႈန္းျဖင့္ ေခၚဆို၍ FunTone အေကာင့္အား စီမံခန္႔ခြဲႏိုင္ပါသည္။"
			sSubscription = "*3333#"
			sUnsubscription = "*3333*0#"
		Case "41406"' Telenor
			sName = "My Tune"
			sDescription = sDescription & "- တစ္ပတ္စာဝန္ေဆာင္ခ ၃၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ အပတ္စဥ္ေကာက္ခံသြားပါလိမ့္မည္။<br>- ေတးသြားတစ္ပုဒ္လွ်င္ ႏွစ္စဥ္ ၃၀၀ က်ပ္ႏႈန္းျဖင့္ ငွားရမ္းႏိုင္ပါသည္။<br>- My Tune IVR နံပါတ္ ၃၃၃ သို႔ တစ္မိနစ္လွ်င္ ၅၀ က်ပ္ႏႈန္းျဖင့္ ေခၚဆို၍ My Tune အေကာင့္အား စီမံခန္႔ခြဲႏိုင္ပါသည္။"
			sPhoneNumber = "333"
			sSubscription = "MT ON"
			sUnsubscription = "MT OFF"
		Case Else' MECTel
			sName = "Hello Music"
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၂၀၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။<br>- ေတးသြားတစ္ပုဒ္လွ်င္ ၃၀၀ က်ပ္ႏႈန္းျဖင့္ ဝယ္ယူႏိုင္ပါသည္။<br>- Hello Music IVR နံပါတ္ ၃၃၃ သို႔ တစ္မိနစ္လွ်င္ ပံုမွန္ဖုန္းေခၚဆိုခႏႈန္းျဖင့္ ေခၚဆို၍ Hello Music အေကာင့္အား စီမံခန္႔ခြဲႏိုင္ပါသည္။"
			sPhoneNumber = "333"
			sSubscription = "register"
			sUnsubscription = "unregister"
	End Select
	
	VAS(sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription)
End Sub

Sub SMSChannels
	StartActivity(SMSChannelsActivity)
End Sub

Sub CLIR
	Dim sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription As String
	
	sName = "CLIR"
	sDescription = "- ဤဝန္ေဆာင္မႈသည္ မိတ္ေဆြေခၚဆိုမည့္ အျခားဖုန္းမ်ားတြင္ ေခၚဆိုသူနံပါတ္ေဖာ္ျပျခင္းကို ေရွာင္ရွားလိုပါက အသံုးျပဳႏိုင္သည္။ သတိျပဳရန္မွာ ေအာ္ပေရတာတူ ဖုန္းအခ်င္းခ်င္းသာ အျပည့္အဝအလုပ္လုပ္ေဆာင္ပါမည္။<br>"
	Select SIMOperator
		Case "41405"' Ooredoo
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၂၅၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။"
			sSubscription = "*311#"
			sUnsubscription = "*311*0#"
	End Select
	
	VAS(sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription)
End Sub

Sub CallForwarding
	Dim sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription As String
	
	sName = "Call Forwarding"
	sDescription = "- ဤဝန္ေဆာင္မႈသည္ မိတ္ေဆြထံေခၚဆိုေသာ အဝင္ဖုန္းမ်ားကို မိတ္ေဆြေရြးခ်ယ္သတ္မွတ္ထားသည့္ အျခားဖုန္းနံပါတ္တစ္ခုသို႔ လႊဲေျပာင္းေပးႏိုင္ပါသည္။<br>"
	Select SIMOperator
		Case "41401"' MPT
			sDescription = sDescription & "- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္သည္။"
			sPhoneNumber = "1331"
			sSubscription = "CF"
			sUnsubscription = "CF OFF"
		Case "41404"' MPT CDMA
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၉၀၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။"
			sPhoneNumber = "1331"
			sSubscription = "Orderdata CF"
			sUnsubscription = "Cancel CF"
	End Select
	
	VAS(sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription)
End Sub

Sub CallWaiting
	Dim sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription As String
	
	sName = "Call Waiting"
	sDescription = "- ဤဝန္ေဆာင္မႈသည္ မိတ္ေဆြဖုန္းေျပာဆိုေနစဥ္အတြင္း ထပ္မံေရာက္ရွိလာေသာ အဝင္ဖုန္းကို လက္ခံေျပာဆိုႏိုင္ေစၿပီး၊ လက္ရွိေျပာဆိုေနမႈကို ေစာင့္ဆိုင္းခိုင္းထားႏိုင္ပါသည္။ ဖုန္းေခၚဆိုမႈႏွစ္ခုအၾကားတြင္လည္း ဖုန္းခ်စရာမလိုပဲ အျပန္အလွန္ေျပာဆိုႏုိင္ပါသည္။<br>"
	Select SIMOperator
		Case "41401"' MPT
			sDescription = sDescription & "- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္သည္။"
			sPhoneNumber = "1331"
			sSubscription = "CW"
			sUnsubscription = "CW OFF"
		Case "41404"' MPT CDMA
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၅၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။"
			sPhoneNumber = "1331"
			sSubscription = "Orderdata CW"
			sUnsubscription = "Cancel CW"
	End Select
	
	VAS(sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription)
End Sub

Sub MissedCallAlert
	Dim sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription As String
	
	sName = "Missed Call Alert"
	sDescription = "- ဤဝန္ေဆာင္မႈသည္ မိတ္ေဆြ၏ဖုန္း စက္ပိတ္ထားသည့္အခ်ိန္တြင္ျဖစ္ေစ၊ ဆက္သြယ္မႈဧရိယာျပင္ပသို႔ ေရာက္ရွိေနသည့္အခ်ိန္တြင္ျဖစ္ေစ၊ မိတ္ေဆြထံေခၚဆိုရန္ႀကိဳးစားေသာ အဝင္ဖုန္းမ်ားအေရအတြက္ကို မိတ္ေဆြ၏ဖုန္း ကြန္ရက္ေပၚသို႔ျပန္လည္ေရာက္ရွိလာေသာအခါ SMS ျဖင့္ သတိေပးခ်က္အျဖစ္ ပို႔ေပးမည္ျဖစ္သည္။<br>"
	Select SIMOperator
		Case "41401"' MPT
			sDescription = sDescription & "- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္သည္။"
			sPhoneNumber = "1331"
			sSubscription = "MCA"
			sUnsubscription = "MCA OFF"
		Case "41406"' Telenor
			sDescription = sDescription & "- တစ္ပတ္စာဝန္ေဆာင္ခ ၃၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ အပတ္စဥ္ေကာက္ခံသြားပါလိမ့္မည္။"
			sPhoneNumber = "222"
			sSubscription = "MCA ON"
			sUnsubscription = "MCA OFF"
		Case Else' MECTel
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၁၀၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။"
			sPhoneNumber = "233"
			sSubscription = "Open MCA"
			sUnsubscription = "Cancel MCA"
	End Select
	
	VAS(sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription)
End Sub

Sub VoiceMail
	Dim sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription As String
	
	sName = "Voice Mail"
	sDescription = "- ဤဝန္ေဆာင္မႈသည္ မိတ္ေဆြဖုန္းမကိုင္ႏိုင္သည့္အခ်ိန္တြင္ မိတ္ေဆြထံသို႔ ဖုန္းေခၚဆိုသူမ်ားသည္ အသံျဖင့္ အမွာစကားခ်န္ထားခဲ့ႏိုင္ပါသည္။<br>"
	Select SIMOperator
		Case "41401"' MPT
			sDescription = sDescription & "- ဝန္ေဆာင္ခ အခမဲ့ ျဖစ္သည္။<br>- Voice Mail IVR နံပါတ္ ၁၅၅၀ သို႔ တစ္မိနစ္လွ်င္ ပံုမွန္ဖုန္းေခၚဆိုခႏႈန္းျဖင့္ ေခၚဆို၍ Voice Mail မ်ားအား စစ္ေဆးႏိုင္ပါသည္။"
			sPhoneNumber = "1331"
			sSubscription = "VMS"
			sUnsubscription = "VMS OFF"
		Case "41406"' Telenor
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၁၀၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။<br>- Voice Mail IVR နံပါတ္ ၂၀၀ သို႔ တစ္မိနစ္လွ်င္ ၅၀ က်ပ္ႏႈန္းျဖင့္ ေခၚဆို၍ Voice Mail မ်ားအား စစ္ေဆးႏိုင္ပါသည္။"
			sSubscription = "*979*2*3*1*1#"
			sUnsubscription = "*979*2*3*2*1#"
		Case Else' MECTel
			sDescription = sDescription & "- တစ္လစာဝန္ေဆာင္ခ ၁၀၀၀ က်ပ္ က်သင့္မည္ျဖစ္ၿပီး၊ ဖုန္းလက္က်န္ေငြထဲမွ လစဥ္ေကာက္ခံသြားပါလိမ့္မည္။<br>- Voice Mail IVR နံပါတ္ ၂၄၄၀ သို႔ တစ္မိနစ္လွ်င္ ပံုမွန္ဖုန္းေခၚဆိုခႏႈန္းျဖင့္ ေခၚဆို၍ Voice Mail မ်ားအား စစ္ေဆးႏိုင္ပါသည္။"
			sPhoneNumber = "233"
			sSubscription = "Open VMS"
			sUnsubscription = "Cancel VMS"
	End Select
	
	VAS(sName, sDescription, sPhoneNumber, sSubscription, sUnsubscription)
End Sub

Sub lvwStores_ItemClick (Position As Int, Value As Object)
	Select Value
		Case 1 : Call(Array As String("06326711", "06327223"))
		Case 2 : Call(Array As String("06325452", "06326171"))
		Case 3 : Call(Array As String("06323692", "06328193"))
		Case 4 : Call(Array As String("06521547"))
	End Select
End Sub

Sub lvwAbout_ItemClick (Position As Int, Value As Object)
	Select Value
		Case 2 : Call(Array As String("09450049060", "0943165226"))
	End Select
End Sub

Sub GetPackageName As String
	Dim r As Reflector
	Return r.GetStaticField("anywheresoftware.b4a.BA", "packageName")
End Sub

Sub Call(PhoneNumber() As String)
	Dim bd As BitmapDrawable
	Dim iResult As Int = 0
	
	If PhoneNumber.Length > 1 Then
		Dialog.Options.Dimensions.Width = 100%x
		Dialog.Options.Dimensions.Height = (PhoneNumber.Length * 60dip) + ((PhoneNumber.Length - 1) * 1dip)
		Dialog.Options.Elements.Title.Typeface = SmartZawgyi
		Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
		Dim DialogLayout As DialogViewLayout = Dialog.LoadLayout("ListDialog")
		Dim lvwList As ListView = DialogLayout.Views.Get("lvwList")
		
		lvwList.TwoLinesAndBitmap.Label.Height = 60dip
		lvwList.TwoLinesAndBitmap.Label.Gravity = Gravity.CENTER_VERTICAL
		lvwList.TwoLinesAndBitmap.Label.TextColor = Colors.DarkGray
		
		bd = xml.GetDrawable("17301645")
		
		For i = 0 To PhoneNumber.Length - 1
			lvwList.AddTwoLinesAndBitmap(PhoneNumber(i), "", bd.Bitmap)
		Next
		
		iResult = DialogLayout.Show("ေခၚဆိုမည့္ဖုန္းနံပါတ္ေရြးခ်ယ္ရန္", "", "", "ပယ္ဖ်က္ပါ", Null)
		If iResult = DialogResponse.CANCEL Then Return
	End If
	
	StartActivity(pc.Call(su.EncodeUrl(PhoneNumber(iResult), "UTF8")))
End Sub

Sub Plan(Name As String, Description As String, PhoneNumber As String, Activation As String, Deactivation As String)
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Message.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Dim sDeactivate As String
	If Deactivation.Length > 0 Then sDeactivate = "Deactivate"
	Dim iResult As Int = Dialog.Msgbox(Name, Description, "Activate", sDeactivate, "ပယ္ဖ်က္ပါ", Null)
	If iResult = DialogResponse.POSITIVE Then
		If PhoneNumber.Length > 0 Then
			ps.Send(PhoneNumber, Activation)
		Else
			StartActivity(pc.Call(su.EncodeUrl(Activation, "UTF8")))
		End If
	Else If iResult = DialogResponse.NEGATIVE Then
		If PhoneNumber.Length > 0 Then
			ps.Send(PhoneNumber, Deactivation)
		Else
			StartActivity(pc.Call(su.EncodeUrl(Deactivation, "UTF8")))
		End If
	End If
End Sub

Sub VAS(Name As String, Description As String, PhoneNumber As String, Subscription As String, Unsubscription As String)
	Dialog.Options.Elements.Title.Typeface = SmartZawgyi
	Dialog.Options.Elements.Message.Typeface = SmartZawgyi
	Dialog.Options.Elements.Buttons.Default.Style.Typeface = SmartZawgyi
	Dim iResult As Int = Dialog.Msgbox(Name, Description, "Subscribe", "Unsubscribe", "ပယ္ဖ်က္ပါ", Null)
	If iResult = DialogResponse.POSITIVE Then
		If PhoneNumber.Length > 0 Then
			ps.Send(PhoneNumber, Subscription)
		Else
			StartActivity(pc.Call(su.EncodeUrl(Subscription, "UTF8")))
		End If
	Else If iResult = DialogResponse.NEGATIVE Then
		If PhoneNumber.Length > 0 Then
			ps.Send(PhoneNumber, Unsubscription)
		Else
			StartActivity(pc.Call(su.EncodeUrl(Unsubscription, "UTF8")))
		End If
	End If
End Sub
